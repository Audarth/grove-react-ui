plugins {
  id "net.saliman.properties" version "1.4.6"
  id "com.marklogic.ml-gradle" version "3.6.1"
}

repositories {
  jcenter()

  // Needed for mlcp dependencies
  maven { url "https://developer.marklogic.com/maven2/" }

  // Needed for hadoop dependencies for mlcp
  maven { url "http://repository.cloudera.com/artifactory/cloudera-repos/" }
}

configurations {
	// This configuration captures the dependencies for running mlcp (Content Pump). This is only needed if you want
	// to run mlcp via Gradle tasks. If you do, using com.marklogic.gradle.task.MlcpTask is a useful starting point, as
	// shown below.  Need to force to use certain version of xml-apis library.
	mlcp {
		resolutionStrategy {
			force "xml-apis:xml-apis:1.4.01"
		}
	}
}

dependencies {
  mlcp "com.marklogic:mlcp:9.0.2"

  /** mlcp uses Log4j for logging, and if Log4j can't find a configuration
   * file, it will complain and you'll get none of mlcp's usually-useful logging.
   * It is recommended then that your Gradle configuration for mlcp include a
   * directory or some other resource that provides a log4j.properties file.
   */
  mlcp files('lib')
}

task loadSampleData(type: com.marklogic.gradle.task.MlcpTask) {
    classpath = configurations.mlcp
    command = "IMPORT"
    database = mlAppConfig.contentDatabaseName
    input_file_path = "data/sample-data.zip"
    input_compressed = true
    output_collections = "data,data/people"
    output_permissions = "rest-reader,read,rest-writer,update"
    output_uri_replace = ".*zip,'/sample/people'"
}
